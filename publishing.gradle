apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {

    archiveClassifier.set("sources")

    if (plugins.hasPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else if (plugins.hasPlugin("java") || plugins.hasPlugin("java-library") || plugins.hasPlugin("kotlin")) {
        from sourceSets.main.allSource
    } else {
        throw new GradleException("No suitable plugin for sourcesJar in module: ${project.name}")
    }
}

afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {

                if (plugins.hasPlugin("com.android.library")) {
                    from(components.release)
                } else {
                    from(components.java)
                    artifact(sourcesJar) {
                        builtBy(sourcesJar)
                    }
                    tasks.named("generateMetadataFileForReleasePublication") {
                        dependsOn(sourcesJar)
                    }
                }

                groupId = 'com.github.hoanganhtuan95ptit.core'
                version = '1.3.39.beta7'
                artifactId = project.name
            }
        }
        repositories { mavenLocal() }
    }
}